include "memory.bfm"

frame Stack {
    marker data
}

macro stack_init(stack: Stack) {
    # Set initial end marker
    stack.data -
}

macro stack_push(stack: Stack, value) {
    # Set start marker
    stack.marker -

    moving {
        # Move to the end marker
        >+[->+]

        # Set the new end marker
        >-

        # Move back to the start marker
        <<+[
            # Shift each element forward one
            -
            [>+<-]
            <+
        ]
    }

    # Move value to the top of the stack
    move(value, stack.data)
}

macro stack_pop(stack: Stack, value) {
    # Move top of the stack into value
    move(stack.data, value)

    # Set start marker
    stack.marker -

    moving {
        # Move to the end marker
        >+[
            -

            # Shift each element back one
            [<+>-]

            >+
        ]

        # Set new end marker
        <-

        # Move back to the start marker
        <+[-<+]
    }
}
